/*
 * build
 * Copyright (c) 2018-2019 Federico Berti
 * Last modified: 07/10/19 13:09
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */


def dateString = getDate()
group 'com.fbdev.helios'

//version dateString
version dateString + '-SNAPSHOT'
def jarNameHead = rootProject.name + "-" + version


apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // https://mvnrepository.com/artifact/net.java.jinput/jinput
    compile group: 'net.java.jinput', name: 'jinput', version: '2.0.9'
    // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
    // https://mvnrepository.com/artifact/com.google.guava/guava
    compile group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    compile group: 'org.jctools', name: 'jctools-core', version: '3.3.0'
    // https://mvnrepository.com/artifact/org.digitalmediaserver/cuelib-core
    compile group: 'org.digitalmediaserver', name: 'cuelib-core', version: '2.0.0'
    compile group: 'com.github.trilarion', name: 'java-vorbis-support', version: '1.2.1'
    // https://mvnrepository.com/artifact/org.jaudiolibs/audioservers-javasound
    compile group: 'org.jaudiolibs', name: 'audioservers-javasound', version: '2.0.0'

    testCompile group: 'junit', name: 'junit', version: '4.13.2'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.2'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.2'

    //include local jars
    compile fileTree(dir: 'lib', include: ['*.jar'])
    testCompile fileTree(dir: 'lib', include: ['*.jar'])
    testCompile fileTree(dir: 'privateLib', include: ['*.jar'])
}

def getDate() {
    String date = new Date().format('yyMMdd')
    date.substring(0,2) + "." + date.substring(2)
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Helios',
                'Implementation-Version': version,
                'Main-Class': 'omegadrive.SystemLoader'
    }
    baseName = project.name + "-all"//+ version
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from {
        ["CREDITS.md", "README.md", "HISTORY.md", "LICENSE.md", "helios.properties"]
    }
    from("bios"){
        include "*.rom"
    }
    with jar
    doLast {
        println("Creating fatJar in folder: " + destinationDir)
    }
}

task copyRes {
    copy {
        from (".") {
            include "res/**"
        }
        into "$buildDir/packageDir"
        exclude "**/*.col", "**/site/**"
    }
    copy {
        from {
            ["CREDITS.md", "README.md", "HISTORY.md",
             "LICENSE.md", "helios.properties"]
        }
        into "$buildDir/packageDir"
    }
    copy {
        from (".") {
            include "lib/**"
        }
        into "$buildDir/packageDir"
    }
    copy {
        from configurations.runtime
        into "$buildDir/packageDir/lib"
    }
    copy {
        from("privateLib") {
            exclude "misc/**"
            exclude "src/**"
            exclude "jna*.jar"
            exclude "jmx*.jar"
        }
        into "$buildDir/packageDir/lib"
    }
    copy {
        from ("res")
            include "*.properties"
        into "$buildDir/packageDir"
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Helios',
                   'Implementation-Version': version,
                   'Main-Class': 'omegadrive.SystemLoader',
                   'Class-Path': configurations.compile.collect { "lib/" + it.getName() }.join(' ')
    }
    archivesBaseName = rootProject.name
    destinationDir = file("$buildDir/packageDir")
    doLast {
        println("Creating jar in folder: " + destinationDir)
    }
}


task packageDistribution(type: Zip) {
    dependsOn "test", "jar", "copyRes", "writeToFile"
    archiveName = jarNameHead + ".zip"
    destinationDir = file("$buildDir/dist")

    from "$buildDir/packageDir"
    doLast {
        println("Creating package: " + archiveName +
                ", folder: " + destinationDir)
    }
}

task writeToFile {
    String jarName = jarNameHead + ".jar"
    List<String> javaAdvOpts =
            ["-XX:AutoBoxCacheMax=65536", "-XX:-DontCompileHugeMethods", "-XX:+AlwaysCompileLoopMethods"]
    //win: default to busy wait
    List<String> winOpts = ["-Dhelios.busy.wait=true"]
    List<String> sampleBase = [ "java",
                            "-Dlog4j.configurationFile=./log4j2.properties",
                                "-Djinput.enable=true -Djinput.native.location=lib",
                            "-jar", "$jarName"] as String[]
    sampleBase.addAll(1, javaAdvOpts)
    List<String> sampleUx = sampleBase.clone()
    sampleUx.add("\"\$@\"" + "\n")
    List<String> sampleWin = sampleBase.clone()
    sampleWin.addAll(1, winOpts)
    sampleWin.add("\"%1\"" + "\n")
    // create the folders if it does not exist.(otherwise it will throw exception)
    File extraFolder = new File( "${buildDir}/packageDir")
    if( !extraFolder.exists() ) {
        extraFolder.mkdirs()
    }
    // create the file and write text to it.
    new File("${buildDir}/packageDir/launcher.sh").text = sampleUx.join (" ")
    new File("${buildDir}/packageDir/launcher.bat").text = sampleWin.join (" ")
}

test {
    testLogging {
        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}
